{A97 - listGenerator}

quickSort(x:List):List
[* Sortiert die Liste <x> aufsteigend mit dem Sortierverfahren Quick-Sort. *]
::= x;
>>>quickSort(x:MultiElementList):List
::= [pivot --> x.first]
		[lower --> x.rest.smaller(pivot),
		upper --> x.rest.greaterOrEqual(pivot)]
	  lower.quickSort.concat(pivot.notEmptyList(upper.quickSort));

smaller(x:List, ---pivot:Comparable):List
[* Filtert die Werte, die kleiner als <pivot> sind. *]
::= x;
>>>smaller(x:NotEmptyList,pivot:Comparable):List
::= pivot.lessEq(x.first).?(
		then --> x.rest.smaller(pivot),
		else --> x.first.notEmptyList(x.rest.smaller(pivot)));

greaterOrEqual(x:List, ---pivot:Comparable):List
[* Filtert die Werte, die größer oder gleich <pivot> sind. *]
::= x;
>>>greaterOrEqual(x:NotEmptyList, pivot:Comparable):List
::= pivot.lessEq(x.first).?(
		then --> x.first.notEmptyList(x.rest.greaterOrEqual(pivot)),
		else --> x.rest.greaterOrEqual(pivot));